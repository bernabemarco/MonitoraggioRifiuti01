
CREATE PROCEDURE [dbo].[PROC_CREA_INDICEROTAZIONE](@DATAODIERNA DATETIME)
AS

BEGIN

	SET NOCOUNT ON

	DECLARE @CODDEPOSITO	AS	nVARCHAR(10)
	DECLARE @CODART			AS	nVARCHAR(50)
	DECLARE @UM1			AS	nVARCHAR(3)
	DECLARE @DATAMOV		AS	DATETIME
	DECLARE @SUMCARICO		AS	DECIMAL(18,6)
	DECLARE @SUMSCARICO		AS	DECIMAL(18,6)
	DECLARE @SUMSOLOCARICHI	AS	DECIMAL(18,6)
	DECLARE @PREZZOTOT		AS	DECIMAL(18,6)
	DECLARE @SUMSCORTA		AS	DECIMAL(18,6)
	DECLARE @GIACENZAINIZ	AS	DECIMAL(18,6)

	DECLARE @CODDEPOSITOPREV AS	nVARCHAR(10)
	DECLARE @CODARTPREV		 AS	nVARCHAR(50)
	DECLARE @DATAMOVPREV	 AS	DATETIME

	DECLARE @SUMCARICOTOT	 AS	DECIMAL(18,6)
	DECLARE @SUMSCARICOTOT	 AS	DECIMAL(18,6)
	DECLARE @SUMSCORTATOT	 AS	DECIMAL(18,6)
	DECLARE @SUMSCORTAGIORNI AS	DECIMAL(18,6)
	DECLARE @SUMSCORTAMEDIA  AS	DECIMAL(18,6)
	DECLARE @DIFFGIORNI		 AS	INT
	DECLARE @DIFFGIORNITOT	 AS	INT

	DECLARE @INDICEROTAZIONE AS DECIMAL(19,6)

	SET @SUMCARICOTOT = 0
	SET @SUMSCARICOTOT = 0
	SET @SUMSCORTATOT = 0
	SET @SUMSCORTAGIORNI = 0
	SET @SUMSCORTAMEDIA = 0
	SET @DIFFGIORNI = 0
	SET @CODDEPOSITOPREV = ''
	SET @CODARTPREV = ''
	SET @DATAMOVPREV = CONVERT(nVarchar(8),CAST(YEAR(@DATAODIERNA) AS nVarchar(4)) + '0101',112)
	SET @INDICEROTAZIONE = 0
	
	UPDATE TP_GIACENZE SET INDROT = 0

	DECLARE Articoli_Cursor CURSOR FAST_FORWARD FOR
		SELECT
			CODDEPOSITO, 
			CODART, 
			UM1, 
			DATAMOV, 
			SUMCARICO,   
			SUMSCARICO, 
			SUMSOLOCARICHI, 
			PREZZOTOT,
			SUMSCORTA,
			GIACENZAINIZIALE
		FROM
		(
		SELECT
			A.CODDEPOSITO, 
			A.CODART, 
			A.UM1, 
			A.DATAMOV, 
			A.SUMCARICO,
			A.SUMSCARICO,
			A.SUMSOLOCARICHI,
			A.PREZZOTOT,
			(A.SUMCARICO - A.SUMSCARICO) AS SUMSCORTA,
			A.GIACENZAINIZIALE
		FROM
			(
			SELECT 
				VSB_1.CODDEPOSITO, 
				VSB_1.CODART, 
				VSB_1.UM1, 
				VSB_1.DATAMOV, 
				Sum(VSB_1.carico-VSB_1.resodacarico) SUMCARICO,   
				sum(VSB_1.scarico-VSB_1.resodascarico) SUMSCARICO, 
				Sum(VSB_1.carico) SUMSOLOCARICHI, 
				Sum(VSB_1.VALORECARICOEURO) PREZZOTOT,
				ISNULL(VSB_2.GIACENZAINIZIALE,0) GIACENZAINIZIALE
			FROM 
				VISTASTORICOMAGBASE VSB_1
			LEFT JOIN
				(SELECT 
					VSB_0.CODART,
					VSB_0.CODDEPOSITO, 
					Sum(VSB_0.carico-VSB_0.scarico+VSB_0.resodascarico-VSB_0.resodacarico) GIACENZAINIZIALE 
				FROM 
					vistastoricomagbase VSB_0 
				WHERE 
					VSB_0.GIACENZA <> 0 AND 
					CONVERT(nVarchar(8),VSB_0.DATAMOV,112)<CONVERT(nVarchar(8),CAST(YEAR(@DATAODIERNA) AS nVarchar(4)) + '0101',112)
				GROUP BY 
					VSB_0.CODART,
					VSB_0.CODDEPOSITO) VSB_2
			ON
				VSB_2.CODART = VSB_1.CODART AND
				VSB_2.CODDEPOSITO = VSB_1.CODDEPOSITO
			WHERE 
				CONVERT(nVarchar(8),VSB_1.DATAMOV,112) BETWEEN CONVERT(nVarchar(8),CAST(YEAR(@DATAODIERNA) AS nVarchar(4)) + '0101',112) AND 
														 CONVERT(nVarchar(8),@DATAODIERNA,112) AND 
				VSB_1.GIACENZA <> 0 
			GROUP BY 
				VSB_1.CODART, 
				VSB_1.CODDEPOSITO, 
				VSB_1.UM1, 
				VSB_1.DATAMOV,
				VSB_2.GIACENZAINIZIALE
			) A
		) DatiIniziali
		ORDER BY CODART, CODDEPOSITO, UM1, DATAMOV
	OPEN Articoli_Cursor	
	FETCH NEXT FROM Articoli_Cursor 
	INTO @CODDEPOSITO,@CODART,@UM1,@DATAMOV,@SUMCARICO,@SUMSCARICO,@SUMSOLOCARICHI,@PREZZOTOT,@SUMSCORTA,@GIACENZAINIZ
	WHILE @@FETCH_STATUS = 0
		BEGIN
			IF (@CODARTPREV <> @CODART) OR (@CODDEPOSITOPREV <> @CODDEPOSITO)
				BEGIN
					IF (@CODARTPREV <> '') OR (@CODDEPOSITOPREV <> '')
						BEGIN
							/*GOSUB NEL CALCOLO DI GIGI*/
							SET @DIFFGIORNI = DATEDIFF(dd, @DATAMOVPREV, CONVERT(nVarchar(8),@DATAODIERNA,112))
							IF @DIFFGIORNI > 0
								BEGIN
									SET @SUMSCORTAGIORNI = @SUMSCORTAGIORNI + (@SUMSCORTATOT * @DIFFGIORNI)
									SET @DIFFGIORNITOT = @DIFFGIORNITOT + @DIFFGIORNI
								END
							/***************************/
							
							/*CALCOLO INDICE DI ROTAZIONE*/
							IF @DIFFGIORNITOT > 0
								BEGIN
									SET @SUMSCORTAMEDIA = @SUMSCORTAGIORNI / @DIFFGIORNITOT
									IF @SUMSCORTAMEDIA <> 0
										BEGIN
											SET @INDICEROTAZIONE = @SUMSCARICOTOT / @SUMSCORTAMEDIA
											
											IF @INDICEROTAZIONE < 0
												BEGIN
													SET @INDICEROTAZIONE = 0
												END
											--PRINT @INDICEROTAZIONE

											UPDATE TP_GIACENZE SET INDROT = @INDICEROTAZIONE WHERE CODARTICOLO = @CODARTPREV AND CODDEPOSITO = @CODDEPOSITOPREV
										END
								END
							/*****************************/
						END
		
					SET @SUMCARICOTOT = 0
					SET @SUMSCARICOTOT = 0
					SET @SUMSCORTATOT = @GIACENZAINIZ
					SET @SUMSCORTAGIORNI = 0
					SET @SUMSCORTAMEDIA = 0
					SET @DIFFGIORNITOT = 0
					SET @DATAMOVPREV = CONVERT(nVarchar(8),CAST(YEAR(@DATAODIERNA) AS nVarchar(4)) + '0101',112)
					SET @INDICEROTAZIONE = 0
				END
			
			SET @SUMCARICOTOT = @SUMCARICOTOT + @SUMCARICO
			SET @SUMSCARICOTOT = @SUMSCARICOTOT + @SUMSCARICO
			
			
			/* GOSUB NEL CALCOLO DI GIGI*/
			SET @DIFFGIORNI = DATEDIFF(dd, @DATAMOVPREV, @DATAMOV)
			IF @DIFFGIORNI > 0
				BEGIN
					SET @SUMSCORTAGIORNI = @SUMSCORTAGIORNI + (@SUMSCORTATOT * @DIFFGIORNI)
					SET @DIFFGIORNITOT = @DIFFGIORNITOT + @DIFFGIORNI
				END
			SET @SUMSCORTATOT = @SUMSCORTATOT + @SUMSCORTA
			/****************************/
			
			SET @CODDEPOSITOPREV = @CODDEPOSITO
			SET @CODARTPREV = @CODART
			SET @DATAMOVPREV = @DATAMOV

			FETCH NEXT FROM Articoli_Cursor 
			INTO @CODDEPOSITO,@CODART,@UM1,@DATAMOV,@SUMCARICO,@SUMSCARICO,@SUMSOLOCARICHI,@PREZZOTOT,@SUMSCORTA,@GIACENZAINIZ
		END
		
	CLOSE Articoli_Cursor
	DEALLOCATE Articoli_Cursor

	/*GOSUB NEL CALCOLO DI GIGI*/
	SET @DIFFGIORNI = DATEDIFF(dd, @DATAMOVPREV, CONVERT(nVarchar(8),@DATAODIERNA,112))
	IF @DIFFGIORNI > 0
		BEGIN
			SET @SUMSCORTAGIORNI = @SUMSCORTAGIORNI + (@SUMSCORTATOT * @DIFFGIORNI)
			SET @DIFFGIORNITOT = @DIFFGIORNITOT + @DIFFGIORNI
		END
	/***************************/

	/*CALCOLO INDICE DI ROTAZIONE*/
	IF @DIFFGIORNITOT > 0
		BEGIN
			SET @SUMSCORTAMEDIA = @SUMSCORTAGIORNI / @DIFFGIORNITOT
			IF @SUMSCORTAMEDIA <> 0
				BEGIN
					SET @INDICEROTAZIONE = @SUMSCARICOTOT / @SUMSCORTAMEDIA
					
					IF @INDICEROTAZIONE < 0
						BEGIN
							SET @INDICEROTAZIONE = 0
						END
					
					UPDATE TP_GIACENZE SET INDROT = @INDICEROTAZIONE WHERE CODARTICOLO = @CODARTPREV AND CODDEPOSITO = @CODDEPOSITOPREV
				END
		END
	/*****************************/

	RETURN

END



GO
GRANT EXECUTE
    ON OBJECT::[dbo].[PROC_CREA_INDICEROTAZIONE] TO [Metodo98]
    AS [dbo];


GO
GRANT REFERENCES
    ON OBJECT::[dbo].[PROC_CREA_INDICEROTAZIONE] TO [Metodo98]
    AS [dbo];

