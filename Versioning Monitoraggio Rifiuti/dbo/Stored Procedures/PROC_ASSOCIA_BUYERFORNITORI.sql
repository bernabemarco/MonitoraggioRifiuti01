

CREATE PROCEDURE [dbo].[PROC_ASSOCIA_BUYERFORNITORI](@Buyer VARCHAR(7))
AS

BEGIN
    SET NOCOUNT ON 
    
    IF EXISTS(select CODICE from TP_ANAGRAFICABUYER where CODICE=@Buyer)
    INSERT INTO TP_CONFIGURAZIONIBUYER(CODBUYER,CODDEPOSITO,CODFORNITORE,TUTTIARTICOLI,UTENTEMODIFICA,DATAMODIFICA)
    select
        @Buyer,
        ARC.CODICE,
        ARC.CODCONTO,
        1,
        USER_NAME(),
        GETDATE()
    from 
        (
            SELECT DISTINCT TLR.CODFOR AS CODCONTO,TLR.TP_CODDEP AS CODICE 
            FROM TABLOTTIRIORDINO TLR
            INNER JOIN
                ANAGRAFICACF ACF
            ON
                ACF.CODCONTO = TLR.CODFOR
            WHERE (TLR.CODFOR IS NOT NULL AND TLR.CODFOR <>'') AND 
            (TLR.TP_CODDEP IS NOT NULL AND TLR.TP_CODDEP <>'')
        ) ARC
    INNER JOIN
        TP_TABMagazzini TTM
    ON
        TTM.CodDeposito = ARC.CODICE
    LEFT JOIN
        TP_CONFIGURAZIONIBUYER TCB
    ON
        TCB.CODFORNITORE = ARC.CODCONTO AND TCB.CODDEPOSITO = ARC.CODICE AND TCB.CODBUYER = @Buyer
    WHERE
        TCB.CODFORNITORE IS NULL AND TCB.CODDEPOSITO IS NULL
END



GO
GRANT EXECUTE
    ON OBJECT::[dbo].[PROC_ASSOCIA_BUYERFORNITORI] TO [Metodo98]
    AS [dbo];

